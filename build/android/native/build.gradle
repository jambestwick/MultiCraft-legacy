apply plugin: 'com.android.library'
import org.ajoberstar.grgit.Grgit

android {
	compileSdkVersion 29
	buildToolsVersion '29.0.3'
	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 29
		externalNativeBuild {
			ndkBuild {
				arguments '-j'
			}
		}
	}

	externalNativeBuild {
		ndkBuild {
			path file('jni/Android.mk')
		}
	}

	// supported architectures
	splits {
		abi {
			enable true
			reset()
			include 'armeabi-v7a', 'arm64-v8a'//, 'x86'
		}
	}

	buildTypes {
		release {
			externalNativeBuild {
				ndkBuild {
					arguments 'NDEBUG=1'
				}
			}
		}
	}

	ndkVersion '21.0.6113669'
}

task cloneGitRepo() {
	def destination = file('deps')
	if(!destination.exists()) {
		def grgit = Grgit.clone(
				dir: destination,
				uri: 'https://github.com/MultiCraft/deps'
		)
		grgit.close()
	}
}

preBuild.dependsOn cloneGitRepo
